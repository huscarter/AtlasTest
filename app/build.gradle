// version define
group = "com.whh"
def apVersion = getEnvValue("apVersion", "");

// plugin define
apply plugin: 'com.android.application'
apply plugin: 'com.taobao.atlas'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId "com.whh.atlastest"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE as int
        versionName VERSION_NAME

        /**
         * 可以使用library的资源文件
         */
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    //签名配置
    signingConfigs {
        debug {
            //
        }

        release {
            storeFile file("test.keystore") //签名证书文件
            storePassword "123456" //签名证书密码
            keyAlias "test" //别名
            keyPassword "123456" //别名密码
        }
    }

    buildTypes {
        debug {// debug model

        }

        release {// release model
            shrinkResources false
            // 是否进行混淆
            minifyEnabled MINIFY_ENABLE as boolean
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

atlas {
    atlasEnabled true
    tBuildConfig {
        /**
         * 自启动bundle配置
         */
//        autoStartBundles = ['com.whh.firstbundle']
        /**
         * 输出成apk
         */
//        outOfApkBundles = ['remotebundle']
        /**
         * 预执行方法className:methodName
         */
//        preLaunch = 'com.whh.atlastest.DemoPreLaunch'
    }

    manifestOptions {
        addAtlasProxyComponents true
    }

    patchConfigs {
        debug {
            createTPatch true
        }

        release {
            createTPatch false
        }
    }

    buildTypes {
        debug {
            if (apVersion) {
                baseApDependency "com.whh:AP-debug:${apVersion}@ap"
                patchConfig patchConfigs.debug
            }
        }

        release {
            if (apVersion) {
                baseApDependency "$group:AP-debug:${apVersion}@ap"
                patchConfig patchConfigs.debug
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    compile "com.android.support:design:$SUPPORT_VERSION"
    compile "com.android.support:support-v4:$SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    compile "com.android.support.constraint:constraint-layout:1.0.2"

    compile 'com.alibaba:fastjson:1.1.45.android@jar'

    // atlas
    compile("com.taobao.android:atlas_core:5.0.7.30@aar") {
        transitive = true
    }
    compile "com.taobao.android:atlasupdate:1.1.4.10@aar"

    // include project
    compile project(":middlewarelibrary")

    // bundle include
    bundleCompile project(':firstbundle') // 此bundle直接被引用，在app里可以直接调用
    bundleCompile project(':remotebundle') // 此bundle在atals的tBuildConfig里被配置成输出型apk，app需下载加载后使用
}

repositories {
    mavenLocal()
    jcenter()
}

/**
 * 获取atlas的版本
 * @param key
 * @param defValue
 * @return
 */
String getEnvValue(key, defValue) {
    def val = System.getProperty(key);
    if (null != val) {
        return val;
    }
    val = System.getenv(key);
    if (null != val) {
        return val;
    }
    return defValue;
}

// to publish
apply plugin: 'maven'
apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            artifact "${project.buildDir}/outputs/apk/${project.name}-debug.ap"
            groupId group
            artifactId "AP-debug"
        }
    }

    repositories {
        mavenLocal()
    }

}
